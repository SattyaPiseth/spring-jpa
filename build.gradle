plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.7'
	id 'io.spring.dependency-management' version '1.1.7'
}

ext {
	mapstructVersion = '1.5.5.Final'
	lombokMapstructBindingVersion = '0.2.0'
	testcontainersVersion = '1.20.2'
}

group = 'co.istad'
version = '0.0.1-SNAPSHOT'
description = 'spring-data-jpa'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation "org.mapstruct:mapstruct:${mapstructVersion}"
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
	annotationProcessor "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructBindingVersion}"
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'com.h2database:h2'
	testImplementation platform("org.testcontainers:testcontainers-bom:${testcontainersVersion}")
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:postgresql'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

sourceSets {
	integrationTest {
		java.setSrcDirs(['src/integrationTest/java'])
		resources.setSrcDirs(['src/integrationTest/resources'])
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output
	}
}

configurations.named('integrationTestImplementation') {
	extendsFrom configurations.testImplementation
}

configurations.named('integrationTestRuntimeOnly') {
	extendsFrom configurations.testRuntimeOnly
}

tasks.register('integrationTest', Test) {
	description = 'Runs Testcontainers integration tests.'
	group = 'verification'
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
	systemProperty 'it.tc', System.getProperty('it.tc', 'true')
	systemProperty 'spring.profiles.active', 'it'
	shouldRunAfter tasks.named('test')
	useJUnitPlatform()
}
